
plugins {
    id 'com.android.library'
    id 'maven-publish'
    id 'signing'
}
android {
    compileSdk 33
    buildToolsVersion '33.0.1'

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 33
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    namespace 'com.yoctopuce.yoctolib'
    publishing {
        singleVariant("release") {
            // if you don't want sources/javadoc, remove these lines
            withSourcesJar()
        }
    }
    signingConfigs {
        release {
            storeFile file("~/AndroidKeyStore")
            storePassword System.getenv("ANDROID_KSTORE_PWD")
            keyPassword System.getenv("ANDROID_KEY_PWD")
            keyAlias  System.getenv("ANDROID_KEY_NAME")
        }
    }
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                groupId 'com.yoctopuce.android'
                artifactId 'YoctoLib'
                version '1.10.65654'
                pom {
                    name = 'YoctoLib'
                    description = 'Yoctopuce library for Android.'
                    url = 'https://github.com/yoctopuce/yoctolib_android'
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    signing {
                        sign publishing.publications.release
                    }
                    developers {
                        developer {
                            id = 'yoctopuce'
                            name = 'Yoctopuce'
                            email = 'https://yoctopuce.com'
                        }
                    }
                    scm {
                        connection = 'scm:git@github.com:yoctopuce/yoctolib_android.git'
                        developerConnection = 'scm:git@github.com:yoctopuce/yoctolib_android.git'
                        url = 'https://github.com/yoctopuce/yoctolib_android'
                    }
                }
            }
        }

        repositories {
            maven {
                name 'nexus'
                url 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
                credentials {
                    username System.getenv("OSSRH_USERNAME")
                    password System.getenv("OSSRH_PASSWORD")
                }
            }
        }
    }
}

signing {
    sign configurations.archives
}