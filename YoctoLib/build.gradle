apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

// This is the library version used when deploying the artifact
version = "1.10.37913"

android {
    compileSdkVersion 29
    buildToolsVersion '29.0.2'

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 29
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}




def siteUrl = 'http://www.yoctopuce.com'      // Homepage URL of the library
def gitUrl = 'https://github.com/yoctopuce/yoctolib_android'   // Git repository URL
group = "com.yoctopuce.android"                                        // Maven Group ID for the artifact


install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'

                // Add your description here
                name 'Yoctopuce Library for Android'
                description = 'Yoctopuce Library for Android'
                url siteUrl

                // Set your license
                licenses {
                    license {
                        name 'Yoctopuce License'
                        url 'http://www.yoctopuce.com/EN/cg.php'
                    }
                }
                developers {
                    developer {
                        id 'yoctopuce'
                        name 'Yoctopuce'
                        email 'dev@yoctopuce.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl

                }
            }
        }
    }
}


task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}



bintray {
    user System.getenv("BINTRAY_USER")
    key System.getenv("BINTRAY_APIKEY")

    configurations = ['archives']
    pkg {
        repo = "maven"
        name = "yoctolib"
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["BSD 2-Clause"]
        publish = false
    }
}


task clearJar(type: Delete) {
    delete 'build/libs/YoctoAPI.jar'
}

task makeJar(type: Copy) {
    from('build/intermediates/bundles/release/')
    into('../Binaries/')
    include('classes.jar')
    rename ('classes.jar', 'YoctoAPI.jar')
}

makeJar.dependsOn(clearJar, build)
